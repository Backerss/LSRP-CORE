#if defined _inc_impl
	#undef _inc_impl
#endif

#include <YSI_Coding\y_hooks>

hook OnPlayerConnect(playerid)
{
	// Spray Tag
	RemoveBuildingForPlayer(playerid, 1490, 2046.41, -1635.84, 13.5859, 0.25);
	RemoveBuildingForPlayer(playerid, 1490, 2066.43, -1652.48, 14.2812, 0.25);
	RemoveBuildingForPlayer(playerid, 1490, 2102.20, -1648.76, 13.5859, 0.25);
	RemoveBuildingForPlayer(playerid, 1490, 2399.41, -1552.03, 28.7500, 0.25);
	RemoveBuildingForPlayer(playerid, 1490, 2353.54, -1508.21, 24.7500, 0.25);
	RemoveBuildingForPlayer(playerid, 1490, 2394.10, -1468.37, 24.7812, 0.25);

	RemoveBuildingForPlayer(playerid, 1531, 1724.73, -1741.50, 14.1016, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1767.21, -1617.54, 15.0391, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1799.13, -1708.77, 14.1016, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1746.75, -1359.77, 16.2109, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1850.01, -1876.84, 14.3594, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1889.24, -1982.51, 15.7578, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1950.62, -2034.40, 14.0938, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1936.88, -2134.91, 14.2188, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 1808.34, -2092.27, 14.2188, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 2273.90, -2265.80, 14.5625, 0.25);
	RemoveBuildingForPlayer(playerid, 1531, 2173.59, -2165.19, 15.3047, 0.25);

	RemoveBuildingForPlayer(playerid, 1530, 2281.46, -1118.96, 27.0078, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2239.78, -999.75, 59.7578, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2122.69, -1060.90, 25.3906, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2062.72, -996.46, 48.2656, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2076.73, -1071.13, 27.6094, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2841.37, -1312.96, 18.8203, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2820.34, -1190.98, 25.6719, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2766.09, -1197.14, 69.0703, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2756.01, -1388.12, 39.4609, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2821.23, -1465.09, 16.5391, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2767.78, -1621.19, 11.2344, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2767.76, -1819.95, 12.2266, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2667.89, -1469.13, 31.6797, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2612.93, -1390.77, 35.4297, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2536.22, -1352.77, 31.0859, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2580.95, -1274.09, 46.5938, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2603.16, -1197.81, 60.9922, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2576.82, -1143.27, 48.2031, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2621.51, -1092.20, 69.7969, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2797.92, -1097.70, 31.0625, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 1332.13, -1722.30, 14.1875, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 1732.73, -963.08, 41.4375, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 1118.91, -2008.24, 75.0234, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2273.20, -2529.12, 8.5156, 0.25);
	RemoveBuildingForPlayer(playerid, 1530, 2704.23, -2144.30, 11.8203, 0.25);

	RemoveBuildingForPlayer(playerid, 1529, 2379.32, -2166.22, 24.9453, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 810.57, -1797.57, 13.6172, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 730.45, -1482.01, 2.2500, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 947.48, -1466.72, 17.2422, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 399.01, -2066.88, 11.2344, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 466.98, -1283.02, 16.3203, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 583.46, -1502.11, 16.0000, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 944.27, -985.82, 39.2969, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 1072.91, -1012.80, 35.5156, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 1206.25, -1162.00, 23.8750, 0.25);
	RemoveBuildingForPlayer(playerid, 1529, 1098.81, -1292.55, 17.1406, 0.25);

	RemoveBuildingForPlayer(playerid, 1528, 2763.00, -2012.11, 14.1328, 0.25);
	RemoveBuildingForPlayer(playerid, 1528, 2794.53, -1906.81, 14.6719, 0.25);
	RemoveBuildingForPlayer(playerid, 1528, 2812.94, -1942.07, 11.0625, 0.25);
	RemoveBuildingForPlayer(playerid, 1528, 2874.50, -1909.38, 8.3906, 0.25);

	RemoveBuildingForPlayer(playerid, 1527, 2182.23, -1467.90, 25.5547, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2132.23, -1258.09, 24.0547, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2233.95, -1367.62, 24.5312, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2224.77, -1193.06, 25.8359, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2119.20, -1196.62, 24.6328, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2522.46, -1478.74, 24.1641, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2322.45, -1254.41, 22.9219, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 1448.23, -1755.90, 14.5234, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 1574.71, -2691.88, 13.6016, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 1071.14, -1863.79, 14.0938, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2392.36, -1914.57, 14.7422, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2430.33, -1997.91, 14.7422, 0.25);
	RemoveBuildingForPlayer(playerid, 1527, 2587.32, -2063.52, 4.6094, 0.25);

	RemoveBuildingForPlayer(playerid, 1525, 1974.09, -1351.16, 24.5625, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2093.76, -1413.45, 24.1172, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1969.59, -1289.70, 24.5625, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1966.95, -1174.73, 20.0391, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1911.87, -1064.40, 25.1875, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2462.27, -1541.41, 25.4219, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2346.52, -1350.78, 24.2812, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2273.02, -1687.43, 14.9688, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2422.91, -1682.30, 13.9922, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1549.89, -1714.52, 15.1016, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1498.63, -1207.35, 24.6797, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1519.42, -1010.95, 24.6094, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1687.23, -1239.12, 15.8125, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1783.97, -2156.54, 14.3125, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1624.62, -2296.24, 14.3125, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2065.44, -1897.23, 13.6094, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 2134.33, -2011.20, 10.5156, 0.25);
	RemoveBuildingForPlayer(playerid, 1525, 1271.48, -1662.32, 20.2500, 0.25);

	RemoveBuildingForPlayer(playerid, 1524, 2162.78, -1786.07, 14.1875, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 2034.40, -1801.67, 14.5469, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 1910.16, -1779.66, 18.7500, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 1837.20, -1814.19, 4.3359, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 1837.66, -1640.38, 13.7578, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 1959.40, -1577.76, 13.7578, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 2074.18, -1579.15, 14.0312, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 2542.95, -1363.24, 31.7656, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 2704.20, -1966.69, 13.7578, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 2489.24, -1959.07, 13.7578, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 1295.18, -1465.22, 10.2812, 0.25);
	RemoveBuildingForPlayer(playerid, 1524, 482.63, -1761.59, 5.9141, 0.25);
}

hook OnMySQLConnected()
{
	inline Handler()
	{
		new 
			rows = cache_num_rows(),
			bool:hasOwner,
			string[256]
		;

		for (new row = 0; row != rows; ++row) {

			if (row == MAX_GRAFFITI_OBJECT) {
				Logger_Err("[rp:features:graffiti] Exceeds the limit",
					Logger_I("num", rows),
					Logger_I("max", MAX_GRAFFITI_OBJECT)
				);
				break;
			}

			cache_get_value_name_int(row, "id", _:Graffiti.Object[row][@id]);

			cache_is_value_name_null(row, "owner_id", hasOwner);
			hasOwner = !hasOwner;

			if (hasOwner) {
				cache_get_value_name_int(row, "owner_id", _:Graffiti.Object[row][@owner]);
				cache_get_value_name(row, "owner", Graffiti.Object[row][@ownerName]);
			} else {
				Graffiti.Object[row][@owner] = INVALID_DB_ID;
				memset(Graffiti.Object[row][@ownerName], 0);
			}

			cache_get_value_name(row, "name", Graffiti.Object[row][@name]);
			cache_get_value_name(row, "font", Graffiti.Object[row][@font]);
			cache_get_value_name_int(row, "modelid", Graffiti.Object[row][@modelid]);
			cache_get_value_name_float(row, "x", Graffiti.Object[row][@x]);
			cache_get_value_name_float(row, "y", Graffiti.Object[row][@y]);
			cache_get_value_name_float(row, "z", Graffiti.Object[row][@z]);
			cache_get_value_name_float(row, "rx", Graffiti.Object[row][@rx]);
			cache_get_value_name_float(row, "ry", Graffiti.Object[row][@ry]);
			cache_get_value_name_float(row, "rz", Graffiti.Object[row][@rz]);

			if (Graffiti.Object[row][@modelid] == 19482) {
				Graffiti.Object[row][@object] = CreateDynamicObject(Graffiti.Object[row][@modelid], Graffiti.Object[row][@x], Graffiti.Object[row][@y], Graffiti.Object[row][@z], Graffiti.Object[row][@rx], Graffiti.Object[row][@ry], Graffiti.Object[row][@rz] + 180, -1, 0, -1, 200 );
	   			
				strcpy(string, Graffiti.Object[row][@name]);
				GraffitiParseBBCode(string);
				SetDynamicObjectMaterialText(Graffiti.Object[row][@object], 0, string, OBJECT_MATERIAL_SIZE_256x256, Graffiti.Object[row][@font], 25, 0, 0xFFFFFFFF, 0, 1 );
			}
			else {
			    Graffiti.Object[row][@object] = CreateDynamicObject(Graffiti.Object[row][@modelid], Graffiti.Object[row][@x], Graffiti.Object[row][@y], Graffiti.Object[row][@z], Graffiti.Object[row][@rx], Graffiti.Object[row][@ry], Graffiti.Object[row][@rz], -1, 0, -1, 200 );
			}

		}
	}

	new query[128];

	mysql_format(
		MySQL_GetHandle(),
		query,
		sizeof query,
		"SELECT g.*, p.player_name AS owner \
		FROM " GRAFFITI_TABLE " g \
		LEFT JOIN " CHARACTER_TABLE " p ON p.c_id = g.owner_id");

	MySQL_PQueryInline(MySQL_GetHandle(), using inline Handler, query);
	return 1;
}

hook OnPlayerDisconnect(playerid, reason)
{
	PlayerSpray{playerid} = false;
}

hook OnGroupInit()
{
	GROUP_ADD<CharacterSelectedGroup>
	{
		@YCMD:graffiti;
		@YCMD:bbcode;
	}

	GROUP_ADD<AllFactionGroup>
	{
		@YCMD:givespray;
		@YCMD:takespray;
	}

	GROUP_ADD<AdminGroup[Y_PLAYER_ADMIN_STAFF_LEVEL - 1]>
	{
		@YCMD:givespray;
		@YCMD:takespray;
	}

	return 1;
}

hook OnPlayerKeyStateChange(playerid, newkeys, oldkeys)
{
	if (newkeys & KEY_FIRE && Graffiti.Setup[playerid][@spraying] == INVALID_GRAFFITI_ID && GetPlayerWeapon(playerid) == WEAPON_SPRAYCAN)
	{
		new faction = INVALID_FACTION_ID;
		GetPlayerFactionMeta(playerid, .faction = faction);
		if (Graffiti.Setup[playerid][@modelid] && (PlayerSpray{playerid} || (faction != INVALID_FACTION_ID && Faction.Meta[faction][@type] == GANG)))
		{
		 	for(new id = 0; id != MAX_GRAFFITI_OBJECT; id++)
			{
			    if (IsPlayerInRangeOfPoint(playerid, 3.0, Graffiti.Object[id][@x], Graffiti.Object[id][@y], Graffiti.Object[id][@z])) {
			        Graffiti.Setup[playerid][@sprayed] = 0;
					Graffiti.Setup[playerid][@spraying] = id;
					stop Graffiti.Setup[playerid][@timer];
					Graffiti.Setup[playerid][@timer] = repeat sprayGraffit(playerid);
			    	break;
		    	}
		    }
			return Y_HOOKS_BREAK_RETURN_1;
		}
	}
	return 1;
}

timer sprayGraffit[1000](playerid)
{
	new
		keys, 
		ud, 
		lr,
		string[256],
		id = Graffiti.Setup[playerid][@spraying]
	;
	GetPlayerKeys(playerid, keys, ud, lr);

	if (keys & KEY_FIRE && IsValidGraffitiObject(id) && GetPlayerWeapon(playerid) == WEAPON_SPRAYCAN && IsPlayerInRangeOfPoint(playerid, 3.0, Graffiti.Object[id][@x], Graffiti.Object[id][@y], Graffiti.Object[id][@z]))
    {
		Graffiti.Setup[playerid][@sprayed]++;

		if (Graffiti.Setup[playerid][@sprayed] >= Graffiti.Setup[playerid][@spraytime])
		{
	    	GameTextForPlayer(playerid, "~g~SPRAYED", 5000, 5);

			strcpy(Graffiti.Object[id][@ownerName], Player_GetCharacterName(playerid));
			Graffiti.Object[id][@owner] = DBID:Player_GetCharacterID(playerid);

			if (Graffiti.Setup[playerid][@font][0] == '\0') {
				strcpy(Graffiti.Setup[playerid][@font], "Diploma");
			}
			strcpy(Graffiti.Object[id][@name], Graffiti.Setup[playerid][@text]);
			strcpy(Graffiti.Object[id][@font], Graffiti.Setup[playerid][@font]);
			Graffiti.Object[id][@modelid] = Graffiti.Setup[playerid][@modelid];
			
			if (IsValidDynamicObject(Graffiti.Object[id][@object])) {
				DestroyDynamicObject(Graffiti.Object[id][@object]);
			}
			
			if (Graffiti.Object[id][@modelid] == 19482) {
				Graffiti.Object[id][@object] = CreateDynamicObject(Graffiti.Object[id][@modelid], Graffiti.Object[id][@x], Graffiti.Object[id][@y], Graffiti.Object[id][@z], Graffiti.Object[id][@rx], Graffiti.Object[id][@ry], Graffiti.Object[id][@rz] + 180, -1, 0, -1, 200 );
	   			
				strcpy(string, Graffiti.Object[id][@name]);
				GraffitiParseBBCode(string);
				SetDynamicObjectMaterialText(Graffiti.Object[id][@object], 0, string, OBJECT_MATERIAL_SIZE_256x256, Graffiti.Object[id][@font], 25, 0, 0xFFFFFFFF, 0, 1 );
			}
			else {
			    Graffiti.Object[id][@object] = CreateDynamicObject(Graffiti.Object[id][@modelid], Graffiti.Object[id][@x], Graffiti.Object[id][@y], Graffiti.Object[id][@z], Graffiti.Object[id][@rx], Graffiti.Object[id][@ry], Graffiti.Object[id][@rz], -1, 0, -1, 200 );
			}

			Streamer_Update(playerid, STREAMER_TYPE_OBJECT);

			Graffiti.Setup[playerid][@sprayed] = 0;
			Graffiti.Setup[playerid][@modelid] = 19482;
			stop Graffiti.Setup[playerid][@timer];
			Graffiti.Setup[playerid][@spraying] = INVALID_GRAFFITI_ID;

			new query[128 + MAX_GRAFFITI_NAME + MAX_GRAFFITI_FONT];
			mysql_format(MySQL_GetHandle(), query, sizeof query, "UPDATE " GRAFFITI_TABLE " SET owner_id = %d, name = '%e', modelid = %d, font = '%e' WHERE id = %d", _:Graffiti.Object[id][@owner], Graffiti.Object[id][@name], Graffiti.Object[id][@modelid], Graffiti.Object[id][@font], _:Graffiti.Object[id][@id]);
			mysql_tquery(MySQL_GetHandle(), query);
	 		return 1;
		}
	
		format(string, sizeof string, "~r~SPRAYING~n~ ~w~%d]", Graffiti.Setup[playerid][@spraytime] - Graffiti.Setup[playerid][@sprayed]);
		GameTextForPlayer(playerid, string, 1000,5 );
	}
	else
	{
		GameTextForPlayer(playerid, "~r~Spray canceled",5000, 5);
		Graffiti.Setup[playerid][@spraying] = INVALID_GRAFFITI_ID;
		stop Graffiti.Setup[playerid][@timer];
	}
	return 1;
}

stock GraffitiParseBBCode(output[], len = sizeof output) {
	strreplace(output, "(n)", "\n", .maxlength = len);
 	strreplace(output, "(r)", INDIANRED, .maxlength = len);
	strreplace(output, "(w)", WHITE, .maxlength = len);
	strreplace(output, "(b)", GRAY2, .maxlength = len);
 	strreplace(output, "(bl)", LIGHTSLATEBLUE, .maxlength = len);
	strreplace(output, "(y)", GOLDENROD2, .maxlength = len);
 	strreplace(output, "(g)", SPRINGGREEN2, .maxlength = len);
	strreplace(output, "(o)", ORANGE2, .maxlength = len);
}


YCMD:bbcode(playerid, params[], help)
{
	SendClientMessage(playerid, X11_WHITE, "(n) = ขึ้นบรรทัดใหม่");
	SendClientMessage(playerid, X11_WHITE, "(r) = สีแดง");
	SendClientMessage(playerid, X11_WHITE, "(w) = สีขาว");
	SendClientMessage(playerid, X11_WHITE, "(b) = สีดำ");
	SendClientMessage(playerid, X11_WHITE, "(bl) = สีน้ำเงิน");
	SendClientMessage(playerid, X11_WHITE, "(y) = สีเหลือง");
	SendClientMessage(playerid, X11_WHITE, "(g) = สีเขียว");
	SendClientMessage(playerid, X11_WHITE, "(o) = สีส้ม");
	return 1;
}

YCMD:graffiti(playerid, params[], help)
{
	new 
		faction = INVALID_FACTION_ID
	;
	GetPlayerFactionMeta(playerid, .faction = faction);

	if (faction == INVALID_FACTION_ID && !PlayerSpray{playerid})
		return SendErrorMsg(playerid, "คุณไม่ได้รับอนุญาต!");
    
	GraffitiMenu(playerid);
	return 1;
}

YCMD:givespray(playerid, params[], help)
{
	new targetid;
	if (sscanf(params,"r", targetid)) 
		return SendSyntaxMsg(playerid, "/givespray <ไอดีผู้เล่น/ชื่อบางส่วน>");

	if (targetid == INVALID_PLAYER_ID) 
		return SendClientMessage(playerid, X11_GRAY71, "   ไม่พบผู้เล่นที่ระบุ");

	new faction = INVALID_FACTION_ID;
	GetPlayerFactionMeta(playerid, .faction = faction);

	if (Faction.Meta[faction][@type] == GANG || Admin_IsPlayerAdminLevel(playerid, Y_PLAYER_ADMIN_STAFF_LEVEL))
	{
		new target_faction = INVALID_FACTION_ID;
		GetPlayerFactionMeta(targetid, .faction = target_faction);

	    if (target_faction == INVALID_FACTION_ID) {

			SendMsgF(playerid, X11_GOLDENROD_2,"> คุณได้อนุณาตให้ %s ใช้สเปรย์", Player_GetCharacterName(targetid));
			SendMsgF(targetid, X11_GOLDENROD_2,"> %s ได้อนุณาตให้คุณใช้สเปรย์(/graffiti)", Player_GetCharacterName(playerid));
			PlayerSpray{playerid} = true;

			Player_RemoveWeaponID(playerid, WEAPON_SPRAYCAN);
			GivePlayerWeapon(targetid, WEAPON_SPRAYCAN, 9999);
		}
		else {
			if (faction == target_faction) {
				Player_RemoveWeaponID(playerid, WEAPON_SPRAYCAN);
				GivePlayerWeapon(targetid, WEAPON_SPRAYCAN, 9999);
			} else {
				SendErrorMsg(playerid, "ไม่สามารถให้สิทธิ์การใช้สเปรย์กับผู้เล่นที่มีแฟคชั่นอยู่แล้วได้");
			}
		}
	}
	else
	{
	   SendErrorMsg(playerid, "คุณต้องอยู่ในแฟคชั่นผิดกฏหมายเพื่อใช้สิ่งนี้");
	   return 1;
	}
	return 1;
}

YCMD:takespray(playerid, params[], help)
{
	new targetid;
	if (sscanf(params,"r",targetid)) return SendSyntaxMsg(playerid, "/takespray <ไอดีผู้เล่น/ชื่อบางส่วน>");
	if (targetid == INVALID_PLAYER_ID) return SendClientMessage(playerid, X11_GRAY71, "   ไม่พบผู้เล่นที่ระบุ");

	new 
		faction = INVALID_FACTION_ID
	;
	GetPlayerFactionMeta(playerid, .faction = faction);

	if (Faction.Meta[faction][@type] == GANG || Admin_IsPlayerAdminLevel(playerid, Y_PLAYER_ADMIN_STAFF_LEVEL))
	{
		new target_faction = INVALID_FACTION_ID;
		GetPlayerFactionMeta(targetid, .faction = target_faction);

		if (PlayerSpray{playerid} && target_faction == INVALID_FACTION_ID) {
	
		    SendMsgF(playerid, X11_GOLDENROD_2,"> คุณได้เพิกถอนสิทธิ์การใช้สเปรย์จาก %s", Player_GetCharacterName(targetid));
			SendMsgF(targetid, X11_GOLDENROD_2,"> %s ได้เพิกถอนสิทธิ์การใช้สเปรย์ของคุณ", Player_GetCharacterName(playerid));
			PlayerSpray{playerid} = false;

			Player_RemoveWeaponID(playerid, WEAPON_SPRAYCAN);
		}
		else {
			if (faction == target_faction) {
				Player_RemoveWeaponID(playerid, WEAPON_SPRAYCAN);
			} else {
				SendErrorMsg(playerid, "ผู้เล่นนั้นยังไม่ได้รับอนุณาตให้ใช้สเปรย์");
			}
		}
	}
	else
	{
	   SendErrorMsg(playerid, "คุณต้องอยู่ในแฟคชั่นผิดกฏหมายเพื่อใช้สิ่งนี้");
	   return 1;
	}
	return 1;
}

GraffitiPickImage(playerid)
{
	new imageName[MAX_GRAFFITI_NAME + 1];

	inline Response(pid, dialogid, response, listitem, string:inputtext[])
	{
		#pragma unused pid, dialogid, response, listitem, inputtext

		if (!response)
			@return GraffitiMenu(playerid);

		GetGraffitiImageName(listitem, imageName);
		SendMsgF(playerid, X11_GOLDENROD_2, "คุณเลือกภาพ: "WHITE"%s", imageName);
		strcpy(Graffiti.Setup[playerid][@text], imageName);
		Graffiti.Setup[playerid][@modelid] = Graffiti.Image[listitem][@modelid];
		Graffiti.Setup[playerid][@spraytime] = 5;
		Graffiti.Setup[playerid][@sprayed] = 0;
		stop Graffiti.Setup[playerid][@timer];
		Graffiti.Setup[playerid][@spraying] = INVALID_GRAFFITI_ID;

		@return 1;
	}

	new dialog_string[MAX_GRAFFITI_IMAGE * MAX_GRAFFITI_NAME + 1];

	for(new i = 0; i != sizeof(GraffitiImage); i++) {
		GetGraffitiImageName(i, imageName);
		format(dialog_string, sizeof dialog_string, "%s\n%s", dialog_string, imageName);
	}

	Dialog_ShowCallback(playerid, using inline Response, DIALOG_STYLE_LIST, "เลือกภาพกราฟฟิตี:", dialog_string, "เลือก", "<< กลับ");
	return 1;
}

GraffitiPickCustomText(playerid) {

	inline Response(pid, dialogid, response, listitem, string:inputtext[])
	{
		#pragma unused pid, dialogid, response, listitem, inputtext

		if (!response)
			@return GraffitiMenu(playerid);

		new len = strlen(inputtext);

	    if (len <= 0 || len > 60) {		    
			SendClientMessage(playerid, X11_GOLDENROD_2, "ข้อความต้องไม่มากกว่า 60 ตัวอักษร");
			@return GraffitiPickCustomText(playerid);
		}

		SendClientMessage(playerid, X11_GOLDENROD_2, "คุณได้ตั้งค่าข้อความของคุณแล้ว");
		strcpy(Graffiti.Setup[playerid][@text], inputtext);
		SendClientMessage(playerid, X11_WHITE, Graffiti.Setup[playerid][@text]);

		Graffiti.Setup[playerid][@spraytime] = len - CE_CountVowel(inputtext);
		Graffiti.Setup[playerid][@sprayed] = 0;
		Graffiti.Setup[playerid][@modelid] = 19482;
		stop Graffiti.Setup[playerid][@timer];
		Graffiti.Setup[playerid][@spraying] = INVALID_GRAFFITI_ID;

		@return 1;
	}

	Dialog_ShowCallback(playerid, using inline Response, DIALOG_STYLE_INPUT, "ต้องการข้อความที่กำหนดเองหรือไม่?:", ""INDIANRED"ข้อแนะ:"WHITE"\n-\tเราใช้เทมเพลต bbcode พิเศษเพื่อจัดรูปแบบข้อความโดยใช้ (c) system ใช้ (n) เพื่อขึ้นบรรทัดใหม่\n\t\t(bl:น้ำเงิน,(w):ขาว,(y):เหลือง,(g):เขียว\n\t\t(b):ดำ, /bbcode สำหรับส่วนที่เหลือ\n\t\tการใช้: นี่จะเป็น(y)คำ(b)สีเหลืองและส่วนที่เหลือจะเป็นสีดำ\n-\tพยายามอย่าเขียนข้อความยาว ๆ\nสูงสุด 60 ตัวอักษร", "เลือก", "<< กลับ");
	return 1;
}

GraffitiMenu(playerid)
{
	inline Response(pid, dialogid, response, listitem, string:inputtext[])
	{
		#pragma unused pid, dialogid, response, listitem, inputtext

		if (response)
		{
			switch(listitem)
			{
				case 0: {
					@return GraffitiPickImage(playerid);
				}
				case 1: {
					@return GraffitiPickCustomText(playerid);
				}
				case 2: {

					inline PickFont(pid2, dialogid2, response2, listitem2, string:inputtext2[])
					{
						#pragma unused pid2, dialogid2, response2, listitem2, inputtext2

						if (!response2) {
							@return GraffitiMenu(playerid);
						}

						switch(listitem2)
						{
							case 2: {
								strcpy(Graffiti.Setup[playerid][@font], "a dripping marker");
							}
							default: {
								strcpy(Graffiti.Setup[playerid][@font], inputtext2);
							}
						}
						
						SendMsgF(playerid, X11_GOLDENROD_2, "คุณเลือกแบบอักษร:"WHITE" %s", inputtext2);
						@return 1;
					}
					@return Dialog_ShowCallback(playerid, using inline PickFont, DIALOG_STYLE_LIST, "เลือกแบบอักษร (สำหรับตั้งข้อความเอง):", "Comic Sans\nLevi Brush\nDripping\nDiploma", "เลือก", "<< กลับ");
				}
			}
		}

		@return 1;
	}

	Dialog_ShowCallback(playerid, using inline Response, DIALOG_STYLE_LIST, "เมนูหลัก:", "เลือกใช้ภาพกราฟฟิตี\nเลือกใช้ตั้งข้อความเอง\nเลือกแบบอักษร (สำหรับตั้งข้อความเอง)", "เลือก", "<< กลับ");
	return 1;
}
